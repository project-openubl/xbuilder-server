package org.openublpe.xmlbuilder.rules.files.sunat;

import org.openublpe.xmlbuilder.core.models.input.sunat.PerceptionRetentionInputModel;
import org.openublpe.xmlbuilder.core.models.input.sunat.PerceptionInputModel;
import org.openublpe.xmlbuilder.core.models.input.sunat.RetentionInputModel;
import org.openublpe.xmlbuilder.core.models.output.sunat.PerceptionRetentionOutputModel;
import org.openublpe.xmlbuilder.core.models.output.sunat.PerceptionOutputModel;
import org.openublpe.xmlbuilder.core.models.output.sunat.RetentionOutputModel;
import java.util.Calendar;
import org.openublpe.xmlbuilder.core.models.catalogs.Catalog;
import org.openublpe.xmlbuilder.core.models.catalogs.Catalog10;
import org.openublpe.xmlbuilder.core.models.catalogs.Catalog1;
import org.openublpe.xmlbuilder.core.models.catalogs.Catalog22;
import org.openublpe.xmlbuilder.core.models.catalogs.Catalog23;
import org.openublpe.xmlbuilder.core.models.output.common.FirmanteOutputModel;
import org.openublpe.xmlbuilder.core.models.output.common.ProveedorOutputModel
import org.openublpe.xmlbuilder.core.models.output.common.ClienteOutputModel;

import function org.openublpe.xmlbuilder.rules.utils.DateUtils.toGregorianCalendarDate;

global java.lang.String DEFAULT_MONEDA;
global org.openublpe.xmlbuilder.core.models.catalogs.Catalog22 DEFAULT_REGIMEN_PERCEPCION;
global org.openublpe.xmlbuilder.core.models.catalogs.Catalog23 DEFAULT_REGIMEN_RETENCION;

dialect "java"

rule "Perception"
when
    $input : PerceptionInputModel()
    $output : PerceptionOutputModel()
then
    $output.setSerieNumero($input.getSerie() + "-" + $input.getNumero());

    Catalog22 regimen = $input.getRegimen() != null
                            ? Catalog.valueOfCode(Catalog22.class, $input.getRegimen()).get()
                            : DEFAULT_REGIMEN_PERCEPCION;
    $output.setRegimen(regimen);
end

rule "Retention"
when
    $input : RetentionInputModel()
    $output : RetentionOutputModel()
then
    $output.setSerieNumero($input.getSerie() + "-" + $input.getNumero());

    Catalog23 regimen = $input.getRegimen() != null
                            ? Catalog.valueOfCode(Catalog23.class, $input.getRegimen()).get()
                            : DEFAULT_REGIMEN_RETENCION;
    $output.setRegimen(regimen);
end


rule "Perception and Retention"
when
    $input : PerceptionRetentionInputModel()
    $output : PerceptionRetentionOutputModel()
then
    // Fecha emision
    long fechaEmision = $input.getFechaEmision() != null ? $input.getFechaEmision() : Calendar.getInstance().getTimeInMillis();
    String fechaEmisionString = toGregorianCalendarDate(fechaEmision);
    $output.setFechaEmision(fechaEmisionString);

    // Moneda
    $output.setMoneda($input.getMoneda() != null ? $input.getMoneda() : DEFAULT_MONEDA);

    // Observacion
    $output.setObservacion($input.getObservacion());

    // Firmante
    FirmanteOutputModel firmante = new FirmanteOutputModel();
    $output.setFirmante(firmante);

    // Proveedor
    ProveedorOutputModel proveedor = new ProveedorOutputModel();
    $output.setProveedor(proveedor);

    // Cliente
    ClienteOutputModel cliente = new ClienteOutputModel();
    $output.setCliente(cliente);

    insert($input.getFirmante());
    insert($input.getProveedor());
    insert($input.getCliente());
    insert(firmante);
    insert(proveedor);
    insert(cliente);
end
