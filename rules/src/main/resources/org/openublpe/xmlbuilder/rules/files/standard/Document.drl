package org.openublpe.xmlbuilder.rules.files.standard;

import org.openublpe.xmlbuilder.core.models.input.standard.DocumentInputModel;
import org.openublpe.xmlbuilder.core.models.output.standard.DocumentOutputModel;
import java.util.Calendar;

import function org.openublpe.xmlbuilder.rules.utils.DateUtils.toGregorianCalendarDate;
import function org.openublpe.xmlbuilder.rules.utils.DateUtils.toGregorianCalendarTime;

global java.lang.String DEFAULT_MONEDA;

dialect "java"

declare DocumentInputOutputModel
   input : DocumentInputModel
   output : DocumentOutputModel
end

rule "DOCUMENT_INIT"
when
    $input : DocumentInputModel()
    $output : DocumentOutputModel()
then
    insert(new DocumentInputOutputModel($input, $output));
end

rule "IO_DOCUMENT"
when
    $inputOutput : DocumentInputOutputModel(
        $input : input,
        $output : output
    )
then
    $output.setMoneda($input.getMoneda() != null ? $input.getMoneda() : DEFAULT_MONEDA);
    $output.setSerieNumero($input.getSerie().toUpperCase() + "-" + $input.getNumero());

    Long fechaEmision = $input.getFechaEmision() != null ? $input.getFechaEmision() : Calendar.getInstance().getTimeInMillis();
    $output.setFechaEmision(toGregorianCalendarDate(fechaEmision));
    $output.setHoraEmision(toGregorianCalendarTime(fechaEmision));

    $output.setObservacion($input.getObservacion());
    $output.setOrdenDeCompra($input.getOrdenDeCompra());
end

rule "IO_DOCUMENT_FECHA_VENCIMIENTO"
when
    $inputOutput : DocumentInputOutputModel(
        $input : input,
        $output : output,
        $input.fechaEmision != null
    )
then
    $output.setFechaVencimiento(
        toGregorianCalendarTime($input.getFechaEmision())
    );
end
