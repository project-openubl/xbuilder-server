package org.openublpe.xmlbuilder.rules.files.standard;

import org.openublpe.xmlbuilder.core.models.input.standard.DocumentInputModel;
import org.openublpe.xmlbuilder.core.models.output.standard.DocumentOutputModel;
import org.openublpe.xmlbuilder.core.models.output.common.ProveedorOutputModel;
import org.openublpe.xmlbuilder.core.models.input.common.ProveedorInputModel
import org.openublpe.xmlbuilder.core.models.output.common.DireccionOutputModel
import org.openublpe.xmlbuilder.core.models.input.common.DireccionInputModel
import org.openublpe.xmlbuilder.core.models.output.common.ContactoOutputModel;

dialect "java"

declare ProveedorInputOutputModel
   input : ProveedorInputModel
   output : ProveedorOutputModel
end

rule "PROVEEDOR_IO_INIT"
when
    $input : DocumentInputModel(proveedor != null)
    $output : DocumentOutputModel(proveedor == null)
then
    $output.setProveedor(new ProveedorOutputModel());

    insert(new ProveedorInputOutputModel($input.getProveedor(), $output.getProveedor()));
end

rule "PROVEEDOR_IO"
when
    $inputOutput: ProveedorInputOutputModel(
        $input : input,
        $output : output
    )
then
    $output.setRuc($input.getRuc());
    $output.setRazonSocial($input.getRazonSocial());
    $output.setNombreComercial($input.getNombreComercial());
end
