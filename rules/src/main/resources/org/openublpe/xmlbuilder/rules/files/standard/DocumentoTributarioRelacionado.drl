package org.openublpe.xmlbuilder.rules.files.standard;

import org.openublpe.xmlbuilder.rules.files.standard.DocumentInputOutputModel;
import org.openublpe.xmlbuilder.core.models.input.standard.DocumentoTributarioRelacionadoInputModel;
import org.openublpe.xmlbuilder.core.models.output.standard.DocumentoTributarioRelacionadoOutputModel;
import org.openublpe.xmlbuilder.core.models.catalogs.Catalog;
import org.openublpe.xmlbuilder.core.models.catalogs.Catalog12;
import java.util.ArrayList;

dialect "java"

declare DocumentoTributarioRelacionadoInputOutputModel
   input : DocumentoTributarioRelacionadoInputModel
   output : DocumentoTributarioRelacionadoOutputModel
end

rule "DOCUMENT_DOCUMENTO_TRIBUTARIO_RELACIONADO_IO_INIT"
when
    $inputOutput : DocumentInputOutputModel(
        $input : input,
        $input.documentosTributariosRelacionados != null,
        $output : output
    )
then
    $output.setDocumentosTributariosRelacionados(new ArrayList<>());
    for (DocumentoTributarioRelacionadoInputModel documentoInput : $input.getDocumentosTributariosRelacionados()) {
        DocumentoTributarioRelacionadoOutputModel documentoOutput = new DocumentoTributarioRelacionadoOutputModel();
        $output.getDocumentosTributariosRelacionados().add(documentoOutput);

        insert(new DocumentoTributarioRelacionadoInputOutputModel(documentoInput, documentoOutput));
    }
end

rule "DOCUMENT_DOCUMENTO_TRIBUTARIO_RELACIONADO_IO_"
when
    $inputOutput: DocumentoTributarioRelacionadoInputOutputModel(
        $input : input,
        $output : output
    )
then
    $output.setSerieNumero($input.getSerieNumero());
    $output.setTipoDocumento(
            Catalog.valueOfCode(Catalog12.class, $input.getTipoDocumento()).get()
    );
end
