package org.openublpe.xmlbuilder.rules.files.standard;

import org.openublpe.xmlbuilder.core.models.input.standard.DocumentInputModel;
import org.openublpe.xmlbuilder.core.models.output.standard.DocumentOutputModel;
import org.openublpe.xmlbuilder.core.models.input.common.FirmanteInputModel;
import org.openublpe.xmlbuilder.core.models.output.common.FirmanteOutputModel;

dialect "java"

declare FirmanteInputOutputModel
   input : FirmanteInputModel
   output : FirmanteOutputModel
end

rule "FIRMANTE_IO_INIT"
when
    $input : DocumentInputModel(
        firmante != null
    )
    $output : DocumentOutputModel(
        firmante == null
    )
then
    $output.setFirmante(new FirmanteOutputModel());

    insert(new FirmanteInputOutputModel($input.getFirmante(), $output.getFirmante()));
end

rule "FIRMANTE_IO_INIT_USING_PROVEEDOR"
when
    $input : DocumentInputModel(
        firmante == null,
        proveedor != null
    )
    $output : DocumentOutputModel(
        firmante == null
    )
then
    FirmanteInputModel firmanteInput = new FirmanteInputModel();
    firmanteInput.setRuc($input.getProveedor().getRuc());
    firmanteInput.setRazonSocial($input.getProveedor().getRazonSocial());

    FirmanteOutputModel firmanteOutput = new FirmanteOutputModel();
    $output.setFirmante(firmanteOutput);

    insert(new FirmanteInputOutputModel(firmanteInput, firmanteOutput));
end

rule "FIRMANTE_IO"
when
    $inputOutput : FirmanteInputOutputModel(
        $input : input,
        $output : output
    )
then
    $output.setRuc($input.getRuc());
    $output.setRazonSocial($input.getRazonSocial());
end
